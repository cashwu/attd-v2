apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: "com.github.erdi.webdriver-binaries"

sourceCompatibility = 8

repositories {
    mavenCentral()
}

configurations {
    providedRuntime
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

sourceSets {
    main {
        resources {
            srcDir '../frontend/build/dist'
        }
    }
}

processResources.dependsOn ':frontend:npmBuild'

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web:2.4.4')
    annotationProcessor('org.projectlombok:lombok:1.18.18')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.18')
    compile('org.projectlombok:lombok:1.18.18')
    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat:2.4.4')
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.4.4')
    testImplementation("io.cucumber:cucumber-java:6.10.2")
    testImplementation("io.cucumber:cucumber-spring:6.10.2")
    testImplementation("org.assertj:assertj-core:3.19.0")
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    testImplementation "org.awaitility:awaitility:4.0.3"
}

task cucumber(type: JavaExec) {
    dependsOn assemble, testClasses
    main = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    environment "SPRING_PROFILES_ACTIVE", "test"
    args = ['--plugin', 'pretty', '--glue', 'com.odde', 'src/test/resources']
}

webdriverBinaries {
    chromedriver '88.0.4324.96'
    configureTask(cucumber)
}

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "dev"
}